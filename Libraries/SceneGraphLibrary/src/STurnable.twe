class STurnable extends SThing {
  STurnable() {
    super();
  }

  void turn(TurnDirection direction,
            Angle amount,
            Duration duration <- Default.duration(),
            AnimationStyle animationStyle <- Default.animationStyle(),
            SThing asSeenBy <- this) {
    localTransformation.animateRotation(axis: direction.getAxis(), angle: amount, asSeenBy: asSeenBy, duration: duration, animationStyle: animationStyle);
  }

  void roll(RollDirection direction,
            Angle amount,
            Duration duration <- Default.duration(),
            AnimationStyle animationStyle <- Default.animationStyle(),
            SThing asSeenBy <- this) {
    localTransformation.animateRotation(axis: direction.getAxis(), angle: amount, asSeenBy: asSeenBy, duration: duration, animationStyle: animationStyle);
  }

  void turnToFace(SThing target,
                  Duration duration <- Default.duration(),
                  AnimationStyle animationStyle <- Default.animationStyle()) {
    localTransformation.animateTurnToFace(target: target, duration: duration, animationStyle: animationStyle);
  }

  void orientToUpright(Duration duration <- Default.duration(),
                       AnimationStyle animationStyle <- Default.animationStyle(),
                       SThing upAsSeenBy <- this.getScene()) {
    localTransformation.animateOrientToUpright(upAsSeenBy: upAsSeenBy, duration: duration, animationStyle: animationStyle);
  }

  void pointAt(SThing target,
               Duration duration <- Default.duration(),
               AnimationStyle animationStyle <- Default.animationStyle(),
               SThing upAsSeenBy <- this.getScene()) {
    localTransformation.animatePointAt(target: target, upAsSeenBy: upAsSeenBy, duration: duration, animationStyle: animationStyle);
  }

  void orientTo(SThing target,
                Duration duration <- Default.duration(),
                AnimationStyle animationStyle <- Default.animationStyle()) {
    localTransformation.animateOrientTo(target: target, duration: duration, animationStyle: animationStyle);
  }

  @TuckedAway void setOrientationRelativeToVehicle(Orientation orientation,
                                                   Duration duration <- Default.duration(),
                                                   AnimationStyle animationStyle <- Default.animationStyle()) {
    this.localTransformation.animateOrientation(endValue: orientation, duration: duration, animationStyle: animationStyle);
  }

  @TuckedAway Orientation getOrientationRelativeToVehicle() {
    return this.localTransformation.getValue().orientation;
  }

  @TuckedAway Orientation getPositionRelativeToVehicle() {
    return this.localTransformation.getValue().position;
  }

  @CompletelyHidden VantagePoint getLocalTransformation() {
    return this.localTransformation.getValue();
  }

  @CompletelyHidden VantagePointProperty localTransformation <- new VantagePointProperty(owner: this, value: VantagePoint.IDENTITY);



  // Tests that could move up to SThing
  Boolean isFacing(SThing other) {
    return other.getTranslation(asSeenBy: this).z < 0.0;
  }

  DecimalNumber getDistanceTo(STurnable other) {
    return this.getTranslation(asSeenBy: other).calculateMagnitude();
  }

  DecimalNumber getDistanceAbove(SThing other, SThing asSeenBy <- this.getScene()) {
    return this.getVerticalGap(lower: other, upper: this, asSeenBy: asSeenBy);
  }

  Boolean isAbove(SThing other, SThing asSeenBy <- this.getScene()) {
    return this.getDistanceAbove(other: other, asSeenBy: asSeenBy) > 0;
  }

  DecimalNumber getDistanceBelow(SThing other, SThing asSeenBy <- this.getScene()) {
    return this.getVerticalGap(lower: this, upper: other, asSeenBy: asSeenBy);
  }

  Boolean isBelow(SThing other, SThing asSeenBy <- this.getScene()) {
    return this.getDistanceBelow(other: other, asSeenBy: asSeenBy) > 0;
  }

  @CompletelyHidden DecimalNumber getVerticalGap(SThing lower, SThing upper, SThing asSeenBy) {
    return this.differenceToEpsilon(a: upper.getMinPoint(asSeenBy: asSeenBy).y,
                                    b: lower.getMaxPoint(asSeenBy: asSeenBy).y);
  }

  DecimalNumber getDistanceToTheRightOf(SThing other, SThing asSeenBy <- other) {
    return this.getLeftRightGap(left: other, right: this, asSeenBy: asSeenBy);
  }

  Boolean isToTheRightOf(SThing other, SThing asSeenBy <- other) {
    return this.getDistanceToTheRightOf(other: other, asSeenBy: asSeenBy) > 0;
  }

  DecimalNumber getDistanceToTheLeftOf(SThing other, SThing asSeenBy <- other) {
    return this.getLeftRightGap(left: this, right: other, asSeenBy: asSeenBy);
  }

  Boolean isToTheLeftOf(SThing other, SThing asSeenBy <- other) {
    return this.getDistanceToTheLeftOf(other: other, asSeenBy: asSeenBy) > 0;
  }

  @CompletelyHidden DecimalNumber getLeftRightGap(SThing left, SThing right, SThing asSeenBy) {
  return this.differenceToEpsilon(a: right.getMinPoint(asSeenBy: asSeenBy).x,
                                  b: left.getMaxPoint(asSeenBy: asSeenBy).x);
  }

  DecimalNumber getDistanceInFrontOf(SThing other, SThing asSeenBy <- other) {
    return this.getFrontBackGap(back: other, front: this, asSeenBy: asSeenBy);
  }

  Boolean isInFrontOf(SThing other, SThing asSeenBy <- other) {
    return this.getDistanceInFrontOf(other: other, asSeenBy: asSeenBy) > 0;
  }

  DecimalNumber getDistanceBehind(SThing other, SThing asSeenBy <- other) {
    return this.getFrontBackGap(back: this, front: other, asSeenBy: asSeenBy);
  }

  Boolean isBehind(SThing other, SThing asSeenBy <- other) {
    return this.getDistanceBehind(other: other, asSeenBy: asSeenBy) > 0;
  }

  @CompletelyHidden DecimalNumber getFrontBackGap(SThing back, SThing front, SThing asSeenBy) {
    //Front and back calculations are flipped because -Z is front
    return this.differenceToEpsilon(a: back.getMinPoint(asSeenBy: asSeenBy).z,
                                    b: front.getMaxPoint(asSeenBy: asSeenBy).z);
  }

  @CompletelyHidden DecimalNumber differenceToEpsilon(DecimalNumber a, DecimalNumber b) {
    DecimalNumber diff <- a - b;
    if($DecimalNumber.abs(number: diff) < .01) {
      return 0;
    }
    return diff;
  }
}
