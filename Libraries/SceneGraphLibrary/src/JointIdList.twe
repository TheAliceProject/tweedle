class JointIdList {
    JointIdList() {
    }

    WholeNumber getCount() {
        return this.count;
    }

    JointIdNode getFirst() {
        return first;
    }

    JointIdNode getLast() {
        return last;
    }

    JointIdNode addFirst(JointId id) {
            JointIdNode jointNode <- new JointIdNode(id: id, next: first);
            first <- jointNode;

            if (last == null) {
                last <- first;
            }

            count <- count + 1;

            return jointNode;
        }

    JointIdNode addLast(JointId id) {
        JointIdNode jointNode <- new JointIdNode(id: id, next: null);

        if (last != null) {
            last.next <- jointNode;
        }
        last <- jointNode;

        if (first == null) {
            first <- last;
        }

        count <- count + 1;

        return jointNode;
    }

    JointIdNode find(JointId id) {
        JointIdNode current <- first;

        while (current != null) {
            if (current.id == id) {
                return current;
            }

            current <- current.next;
        }

        return null;
    }

    @CompletelyHidden JointIdNode first <- null;
    @CompletelyHidden JointIdNode last <- null;
    @CompletelyHidden WholeNumber count <- 0;
}